apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'

group = 'com.spiddekauga.http'
version = '0.2.1'

task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}


configurations {
	deployerJars
	provided
}

sourceSets {
	main { compileClasspath += configurations.provided }
}

dependencies {
	deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

repositories {
	mavenCentral()
}

install  {
	repositories.mavenInstaller {
		pom.artifactId = project.name
	}
}

uploadArchives {
	repositories {
		// Set mavenRepositoryUrl & mavenUserName inside gradle.properties
		mavenDeployer {
			configuration = configurations.deployerJars
 			pom.artifactId = project.name
			repository(url: mavenRepositoryUrl + '/releases') {
				authentication(userName: mavenUserName)
			}
			snapshotRepository(url: mavenRepositoryUrl + '/snapshots') {
				authentication(userName: mavenUserName)
			}
		}
	}
}

eclipse.project {
	name = project.name
}
